---
title: "Tripartite Model 3: Fossilized Birth Death"
output: html_document
---

### David W. Bapst, Laura Soul


#### Lecture & Hands-On with RevBayes/RevNotebook


# Learning Objectives
Students will learn to:
- Describe a stochastic model of branching and extinction
- Explain how allowing for serial sampling allows us to consider trees that encompass the fossil record
- 



MISSION
If students come away with anything from my FBD lecture, it should be
that FBD is a simple, but sensible model of diversification and
sampling, which allows us to relate how well sampled a given fossil
record is to inform how much evolutionary history we may have
ultimately missed, and thus when clades may have diverged in the
shadow before time.

## Pure Birth Models

Simulate under a pure-birth process and ask whose data looks like this? 


Nee 2006

## Birth-Death Models

Simulate under a birth-death process and ask whose data looks like this? 

### How We've Used Birth-Death In Paleontology

something something Foote, per-capita rates



As a note, I do plan on saying that lambda, mu and psi are often given the alternative notation of p/q/r in some paleontological studies -- and then saying nothing more at all.


SEE NOTATION GUIDE - LINK



### Birth-Death Models For Phylogenies

![The Reconstructed Phylogeny ](Reconstructing Phylogenies - Purvis 2008.jpeg)

![How the Nee et al. birth-death model works.](lineage_ages_in_Nee_etal._1994.jpg)

/home/dwbapst/workspace/PaleoSoc_phylo_short_course_2019/vignettes/module_07_TripartiteModel3_Fossilized_Birth_death/lineage_ages_in_Nee_etal._1994.jpg

## Fossilized Birth-Death Model

-Simulate under the FBD and show that this process allows sampled ancestors? 	
	

### Serial Sampling

### Sampled Ancestors

### FBD Ranges Model

![FBD Ranges Model (Stadler, 2017)](tip-dating with ranges Stadler FBD paper 2017.jpg)

# Bringing It All Together

## How long will it run?

A long time.

## Post-Analysis Examination

The MCMC is done. That was the easy part, now its the hard part.

Thinking about the output of Bayesian analyses isn't easy...

Thinking about the output of Phylogenetic  analyses isn't easy...

Thankfully these two problems somewhat solve each other.

### Did it converge?

Tracer

### What did the posterior find?

More Tracer

### Summarizing the tree(s)

> A single tree is not worth the forest.




# NOTES

	- Fossilized Birth Death Models 
		- Stadler, 2010; Heath et al., 2014; Stadler et al., 2018
		- FBD, Range-FBD
	- Anc-Desc Relationships, Sampled Ancestors in Tip-Dating
		- Sampled Ancestor Moves in MCMC
		- Gavryushkina et al. 2014; 2016
	- Try to simplify, do not get lost in covering historical perspective
	
	- From Pete:
		- BD & FBD (BDS) models for putting prior probabilities on tree topologies offer independent check on characters 
		- A. Emphasis: Probabilities getting fossil record given phylogeny based on likelihood of unsampled ancestors AND probability of no sampled sister taxa (aka, Raup’s Revenge).
		- B. Our old friend hyperpriors used to create distributions for these rates.
		- Z. 
		
##########################




#########################
		
From Pete

By the way, here is the FBD parameterization I’ve been using for cinctan analyses.  There is one big difference between this and the RevBayes standard: extinction does not vary independently of origination; instead, turnover is set up to range from 0.90X to 1.05X origination, and extinction is set to the turnover.  (Given that the entire clade goes extinct over this interval, we might even just set mu=lambda….)  However: I have left the “standard” code in there blocked out.  

The estimate for rho is an empirical one based on all echinoderm occurrences from the last stage-slice in the Cambrian in which a cinctan occurs.  

I think that I’ve brought this up before, but there seems to be a bug in RevBayes, too.  If you use min & max as they do, then the program seems to treat the last appearance as the earliest possible divergence time for OTUs rather than the first appearance.  As a result, you get divergence dates for taxa that are after they first appear.  Moreover, you sometimes wind up with positive log-priors in these cases.  I’ll bet my eye-teeth that this is the program using the FA data in that case and coming up with a negative value for the branch duration, and then multiplying that negative by the log of the probability of zero finds or Cousin Matthews over that absolute amount of time.  

This routine also separates out divergence times (node age) and origination times (when the lineage starts).  The latter does not appear to be in the tree model anywhere.  If you were to do lineage-through-time plots, then each observed species would get it’s origination time through it’s last appearance, and each unsampled ancestor (Norell’s “ghost taxa”) would get the origination time through the divergence time.  

The other useful thing about that routine is that you see tau.nodeAge and tau.branchLength, which are useful for other things.

Finally, the FBD_p, FBD_q & FBD_r are not used for anything in the analyses.  I just use them for the onscreen output, as that is alphabetized; this way, you see the “classic” parameters in order as your computer grinds through cinctan trees & rate models.


```
########################################################################
# Set up appropriate parameters for speciation, extinction & sampling  #
#   "Seed" numbers based on analyses of Paleobiology Database data.    #
########################################################################
# Diversification Rates based on Echinodermata
speciation_rate ~ dnExponential(1.471);
# NOTE: If it gets stuck in this script, then set origination & extinction to 1.0
moves.append(mvScale(speciation_rate, lambda=0.01, weight=5));
moves.append(mvScale(speciation_rate, lambda=0.10, weight=3));
moves.append(mvScale(speciation_rate, lambda=1.00, weight=1));

# # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # #
# NOTE: FBD scripts often allow extinction to vary independently of speciation;     #
# However, empirical studies show that these two rates usually are close to equal   #
#               and they definitely are not independent.                            #
# So, here we'll make turnover (ext/orig) an independent variable and use it        #
#               to scale extinction relative to origination                         #
# # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # #
turnover ~ dnUnif(0.9, 1.05);
moves.append(mvSlide(turnover, delta=0.01, weight=5));
moves.append(mvSlide(turnover, delta=0.10, weight=3));
moves.append(mvSlide(turnover, delta=1.00, weight=1));
extinction_rate := turnover*speciation_rate;
diversification := speciation_rate - extinction_rate;

# old extinction stuff. We should not use this, as extinction should not be independent of origination!
#extinction_rate ~ dnExponential(1.471);
#moves.append(mvScale(extinction_rate, lambda=0.01, weight=5));
#moves.append(mvScale(extinction_rate, lambda=0.10, weight=3));
#moves.append(mvScale(extinction_rate, lambda=1.00, weight=1));
#turnover := extinction_rate/speciation_rate;

# Fossil Sampling Rates based on collection occupied by Echinodermata
psi ~ dnExponential(3.892);
completeness := psi/(extinction_rate+psi);
moves.append(mvScale(psi, lambda=0.01, weight=5));
moves.append(mvScale(psi, lambda=0.10, weight=3));
moves.append(mvScale(psi, lambda=1.00, weight=1));

# Proportional Taxon Sampling of Youngest Time Slice
rho <- 0.506;	# 'extant' sampling.

# Establish Basal Divergence Time
origin_time ~ dnUnif(7.3, 12.11);
moves.append(mvSlide(origin_time, delta=0.01, weight=5));
moves.append(mvSlide(origin_time, delta=0.10, weight=3));
moves.append(mvSlide(origin_time, delta=1.00, weight=1));

fbd_dist = dnFBDRP(origin=origin_time, lambda=speciation_rate, mu=extinction_rate, psi=psi, rho=rho, taxa=taxa);

############################################################################
#                               Set up tree                                #
############################################################################
# create the vector of clade constraints
constraints = v(ingroup);
tau ~ dnConstrainedTopology(fbd_dist,constraints=constraints);

moves.append(mvFNPR(tau, weight=n_branches/2));                              # time-tree pruning & grafting
moves.append(mvNNI(tau, weight=n_branches/2));                               # nearest-neighbor interchanges
moves.append(mvCollapseExpandFossilBranch(tau,origin_time,weight=n_taxa/4)); # consider ancestor-descendant rather than sister species
moves.append(mvNodeTimeSlideUniform(tau, weight=n_branches/2));              # adjust divergence times
moves.append(mvRootTimeSlideUniform(tau, origin_time, weight=5));            # adjust basal divergence time.

num_samp_anc := tau.numSampledAncestors();
for (bn in 1:n_branches)	{
	divergence_dates[bn]:=tau.nodeAge(bn)                   # this is when a hypothesized ancestor diverges or an OTU is first seen;
	branch_lengths[bn]:=tau.branchLength(bn);               # this is branch *duration* not expected change!
	origin_dates[bn]:=tau.branchLength(bn)+tau.nodeAge(bn); # this is when a lineage diverged from its ancestor
	}
summed_gaps := sum(branch_lengths);

clade_extant = clade("Sucocystis_acrofera");
age_extant := tmrca(tau, clade_extant);	# There is no particularly good reason to keep this!

pruned_tau := fnPruneTree(tau, prune=v("Asturicystis_havliceki","Nelegerocystis_ivantzovi","Rozanovicystis_triangularis","Davidocinctus_pembrokensis"))
#### for easy printing on screen ####
fbd_p := speciation_rate;
fbd_q := extinction_rate;
fbd_r := psi;
```